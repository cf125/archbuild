# Maintainer: Troy Engel <troyengel+arch@gmail.com>

pkgname=vpn-unlimited
pkgver=2.4.0
pkgrel=1
pkgdesc="VPN client for the VPN Unlimited service"
arch=('x86_64')
url="https://www.vpnunlimitedapp.com/downloadlinux"
license=('custom')
options=('emptydirs')
depends=('qt4' 'libssh2' 'libcurl-compat')
source=("http://apt.simplexsolutionsinc.com/repository/pool/main/v/vpn-unlimited/${pkgname}_${pkgver}_amd64.deb")
md5sums=('0d14a75000af104c88247f5915dfad40')

package() {
  # Unpack the vendor package
  cd "${srcdir}"
  tar -zxf data.tar.gz
  cp -dpr "${srcdir}/usr" "${pkgdir}"
  install -Dm0644 "${pkgdir}/usr/share/doc/vpn-unlimited/copyright" \
    "${pkgdir}/usr/share/licenses/${pkgname}/copyright"

  # Create a custom script that forces the older libcurl
  echo '#!/bin/sh' > "${pkgdir}/usr/bin/vpnu-arch"
  echo 'LD_PRELOAD=libcurl.so.3 vpn-unlimited' >> "${pkgdir}/usr/bin/vpnu-arch"
  chmod 0755 "${pkgdir}/usr/bin/vpnu-arch"

  # Alter the desktop file to launch our script instead
  sed -i 's/Exec=vpn-unlimited/Exec=vpnu-arch/g' "${pkgdir}/usr/share/applications/vpn-unlimited.desktop"

  # Save this for conversion to a systemd unit some day
  cp -p "${srcdir}/etc/init.d/vpn-unlimited-daemon" "${pkgdir}//usr/share/doc/vpn-unlimited/"
}

